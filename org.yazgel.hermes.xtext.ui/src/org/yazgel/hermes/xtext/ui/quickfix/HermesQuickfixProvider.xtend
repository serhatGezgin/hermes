/*
* generated by Xtext
*/
package org.yazgel.hermes.xtext.ui.quickfix

import org.eclipse.xtext.ui.editor.quickfix.DefaultQuickfixProvider
import org.eclipse.xtext.ui.editor.quickfix.Fix
import org.eclipse.xtext.ui.editor.quickfix.IssueResolutionAcceptor
import org.eclipse.xtext.validation.Issue
import org.yazgel.hermes.DataType
import org.yazgel.hermes.Entity
import org.yazgel.hermes.Ref
import org.yazgel.hermes.xtext.validation.HermesValidator
import org.eclipse.xtext.diagnostics.Diagnostic
import org.yazgel.hermes.HermesFactory
import static extension org.eclipse.xtext.EcoreUtil2.*

/**
 * Custom quickfixes.
 *
 * see http://www.eclipse.org/Xtext/documentation.html#quickfixes
 */
class HermesQuickfixProvider extends DefaultQuickfixProvider {

	@Fix(HermesValidator::HIERARCHY_CYCLE)
	def void removeSuperType(Issue issue, IssueResolutionAcceptor acceptor) {
		acceptor.accept(
			issue,
			"Remove super entity",
			'''Remove super entity '«issue.data.get(0)»' ''',
			"delete_obj.gif",
			[ element, context |
				(element as Entity).superEntity = null;
			]
		);
	}

	@Fix(HermesValidator::INVALID_ENTITY_NAME)
	def void capitalizeEntityNameFirstLetter(Issue issue, IssueResolutionAcceptor acceptor) {
		acceptor.accept(
			issue,
			"Capitalize first letter", // label
			"Capitalize first letter of '" + issue.data.get(0) + "'", // description
			"Entity.gif", // icon
			[ element, context |
				(element as Entity).name = issue.data.get(0).toFirstUpper
			]
		);
	}

	@Fix(HermesValidator::INVALID_REFERENCE_NAME)
	def void uncapitalizeReferenceNameFirstLetter(Issue issue, IssueResolutionAcceptor acceptor) {
		acceptor.accept(
			issue,
			"Uncapitalize first letter", // label
			"Uncapitalize first letter of '" + issue.data.get(0) + "'", // description
			"Reference.gif", // icon
			[ element, context |
				(element as Ref).name = issue.data.get(0).toFirstLower
			]
		);
	}

	@Fix(HermesValidator::INVALID_DATATYPE_NAME)
	def void uncapitalizeDataTypeNameFirstLetter(Issue issue, IssueResolutionAcceptor acceptor) {
		acceptor.accept(
			issue,
			"Uncapitalize first letter", // label
			"Uncapitalize first letter of '" + issue.data.get(0) + "'", // description
			"Datatype.gif", // icon
			[ element, context |
				(element as DataType).name = issue.data.get(0).toFirstLower
			]
		);
	}

	@Fix(Diagnostic::LINKING_DIAGNOSTIC)
	def void createMissingEntity(Issue issue, IssueResolutionAcceptor acceptor) {
		acceptor.accept(
			issue,
			"Create missing entity",
			"Create missing entity",
			"Entity.gif",
			[ element, context |
				val currentEntity = element.getContainerOfType(typeof(Entity))
				val model = currentEntity.eContainer as org.yazgel.hermes.Package
				model.ownedEntity.add(
					model.ownedEntity.indexOf(currentEntity) + 1,
					HermesFactory::eINSTANCE.createEntity() => [
						name = context.xtextDocument.get(issue.offset, issue.length)
					]
				)
			]
		);
	}
}
